plugins {
    id 'java'
    id 'org.springframework.boot' version '2.6.13'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'maven-publish'
}

group = 'com.cardanoj'
version = '0.0.4-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web:2.6.13'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.6.13'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withSourcesJar()
    withJavadocJar()
}

tasks.named('test') {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.cardanoj'
            artifactId = 'cardanoj-api'

            from components.java

            pom {
                name.set('CardanoJ API')
                description.set('It simplifies the process for Java developers to integrate Cardano functionalities into their applications.')
                url.set('https://github.com/CardanoJ/cardanoJ')

                licenses {
                    license {
                        name.set('The Apache License, Version 2.0')
                        url.set('http://www.apache.org/licenses/LICENSE-2.0')
                    }
                }
                developers {
                    developer {
                        id.set('Quotus-dev')
                        name.set('Anjali Sethy')
                        email.set('your-email@example.com')
                    }
                }
                scm {
                    connection.set('scm:git:git://github.com/CardanoJ/cardanoJ.git')
                    developerConnection.set('scm:git:ssh://github.com/CardanoJ/cardanoJ.git')
                    url.set('https://github.com/CardanoJ/cardanoJ')
                }
            }
        }
    }

    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri('https://maven.pkg.github.com/CardanoJ/cardanoJ')

            credentials {
                username = project.findProperty('gpr.user') ?: System.getenv('GITHUB_ACTOR')
                password = project.findProperty('gpr.token') ?: System.getenv('GITHUB_TOKEN')
            }
        }
    }
}

wrapper {
    gradleVersion = '7.3'
    distributionType = Wrapper.DistributionType.ALL
}
